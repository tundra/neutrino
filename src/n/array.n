# Copyright 2013 the Neutrino authors (see AUTHORS).
# Licensed under the Apache License, Version 2.0 (see LICENSE).

## The built-in type of runtime-native object arrays.
def @Array := @ctrino.get_builtin_type("Array");

## Creates a new mutable array of the given length with all elements initialized
## to null.
def ($this == @Array).new($length) => @ctrino.new_array($length);

## Invokes the given thunk for each element in this array.
def ($this is @Array).for($thunk) {
  var $i := 0;
  bk
    $callback.keep_running() => $i < ($this.length)
    on.run() {
      $thunk($this[$i]);
      $i := $i + 1;
    }
  in @while($callback);
}

## Returns the $index'th element of this array.
@ctrino.builtin("array[]")
def ($this is @Array)[$index];

## Sets the $index'th element of this array to the specified value.
@ctrino.builtin("array[]:=()")
def ($this is @Array)[$index]:=($value);

## Returns the length of this array.
@ctrino.builtin("array.length")
def ($this is @Array).length;

def type @Array is @Object;
