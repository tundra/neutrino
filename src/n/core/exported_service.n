# Copyright 2015 the Neutrino authors (see AUTHORS).
# Licensed under the Apache License, Version 2.0 (see LICENSE).

# A wrapper around a foreign object within this process that can be accessed as
# if it were a remote.
def @fsi:ExportedService := @ctrino.get_builtin_type("ExportedService");

def type @fsi:ExportedService is @Object;

## Creates and returns a new exported service backed by the given handler where
## method resolution happens in the given module.
def ($this == @fsi:ExportedService).new($handler, $module) => @ctrino.new_exported_service($handler, $module);

# Generic operation on an exported service.
def ($this is @fsi:ExportedService)->(*) as $args => $this.call_with_args($args);

# The native that does the actual work of a remote call.
@ctrino.builtin("exported_service.call_with_args")
def ($this is @fsi:ExportedService).call_with_args($args);

def @IncomingRequestThunk := @ctrino.get_builtin_type("IncomingRequestThunk");

def type @IncomingRequestThunk is @Object;

# Yields the exported service that is the target of this request.
@ctrino.builtin("incoming_request_thunk.handler")
def ($this is @IncomingRequestThunk).handler;

# Yields the module within which to execute this request.
@ctrino.builtin("incoming_request_thunk.module")
def ($this is @IncomingRequestThunk).module;

# Yields the call literal the contains the request data.
@ctrino.builtin("incoming_request_thunk.request")
def ($this is @IncomingRequestThunk).request;

# Yields the promise that was returned as the result of initiating this request.
@ctrino.builtin("incoming_request_thunk.promise")
def ($this is @IncomingRequestThunk).promise;

# Clears the C-allocated backing data from this request.
@ctrino.builtin("incoming_request_thunk.clear!")
def ($this is @IncomingRequestThunk).clear!;

# The entry-point called by the event loop to execute an external request to an
# exported service.
def ($this is @IncomingRequestThunk)() {
  # Extract all the data from the thunk that we need to perform the request.
  def $request := $this.request
    .replace_argument(subject, $this.handler)
    .replace_argument(transport, sync);
  def $module := $this.module;
  def $promise := $this.promise;
  # Dispose the data backing this thunk. From this point on the accessors will
  # no longer work.
  $this.clear!();
  # Perform the actual request.
  def $value := $module.invoke($request);
  $promise.fulfill!($value);
}
